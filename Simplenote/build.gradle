configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:24.1.1'
}

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/repo' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven { url 'http://simperium.github.io/simplenote-android' }
    maven { url 'http://wordpress-mobile.github.io/Android-PasscodeLock'}
    maven { url 'http://wordpress-mobile.github.io/WordPress-Android' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://s3.amazonaws.com/repo.commonsware.com' }
}

dependencies {
    compile 'com.simperium.android:simperium:0.6.8'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:support-vector-drawable:23.3.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.google.android.gms:play-services-wearable:8.3.0'
    compile 'org.wordpress:passcodelock:1.3.0'
    compile 'com.automattic:tracks:1.1.0'
    compile 'com.loopj.android:android-async-http:1.4.9'
    compile 'com.commonsware.cwac:anddown:0.2.4'
    compile 'com.github.dmfs:color-picker:c3b3b52033dc0b33bd831e731ec6f74fb1e8a69a'
    compile 'asia.ivity.android:drag-sort-listview:1.0'

    //espresso
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'

    //Junit
    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito framework
    testCompile 'org.mockito:mockito-core:1.10.19'

   // wearApp project(':Wear')
}

version "1.6.0"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId "com.automattic.simplenote"
        versionName project.version
        versionCode 53
        minSdkVersion 19
        targetSdkVersion 23

        buildConfigField "String", "SIMPERIUM_APP_ID", "\"${project.simperiumAppId}\""
        buildConfigField "String", "SIMPERIUM_APP_KEY", "\"${project.simperiumAppKey}\""
        buildConfigField "String", "GOOGLE_ANALYTICS_ID", "\"${project.googleAnalyticsId}\"";
        buildConfigField "String", "BUILD", "\"${gitDescribe()}\"";
        buildConfigField "String", "BUILD_HASH", "\"${gitHash()}\"";

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"


    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    useLibrary 'org.apache.http.legacy'
    productFlavors {
    }
}

if(["storeFile", "storePassword", "keyAlias", "keyPassword"].count { !project.hasProperty(it) } == 0 ){
    android {
      signingConfigs {
          release {
              storeFile = file(project.storeFile)
              storePassword = project.storePassword
              keyAlias = project.keyAlias
              keyPassword = project.keyPassword
          }
          debug {
              storeFile = file(project.storeFile)
              storePassword = project.storePassword
              keyAlias = project.keyAlias
              keyPassword = project.keyPassword
          }
      }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}

